apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"
apply plugin: "java-gradle-plugin"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation gradleApi()
    implementation localGroovy()
    implementation "com.android.tools.build:gradle:$android_gradle_version"
}

repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
    }
}

final groupId = commonProp['group']
final versionName = commonProp['versionName']
group = groupId
version = versionName

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = groupId
            pom.version = versionName
            pom.artifactId = commonProp['pluginId']
            repository(url: uri('../repo'))
        }
    }
}

final pluginId = "${commonProp['group']}.${commonProp['pluginId']}"

gradlePlugin {
    plugins {
        simplePlugin {
            id = pluginId
            implementationClass = 'com.lyc.appinject.InjectPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/SirLYC/AppInject'
    vcsUrl = 'https://github.com/SirLYC/AppInject.git'
    tags = ['Android', 'Gradle', 'plugin', 'Inject', 'Depence', 'Decouple']
    description = commonProp['desc']

    plugins {
        appInjectPlugin {
            id = pluginId
            displayName = 'AppInject'
        }
    }
}

if (publishPlugin) {
    apply from: 'publish.gradle'
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}
